plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'jacoco'
}

group = 'com.kvaithin'
version = '1.0.3'

repositories {
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/" }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {

        mavenJava(MavenPublication) {

            groupId = 'com.kvaithin'
            artifactId = 'commons-tools'
            version = '1.0.3'
            from components.java

            pom {
                name = 'Commons Tools'
                description = 'Commons Tools Utility'
                url = 'https://github.com/kvaithin/commons-tools'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'kvaithin'
                        name = 'karthik vaithin'
                        email = 'k@kvaithin.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/kvaithin/commons-tools.git'
                    developerConnection = 'scm:git:ssh://github.com/kvaithin/commons-tools.git'
                    url = 'https://github.com/kvaithin/commons-tools'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["username"]
                password = project.properties["password"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/jacoco")
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.90
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.90
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
